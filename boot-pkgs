#!/usr/bin/perl -w

use strict;

use File::Path 'rmtree';
use File::Basename;

my @library_dirs = ();
my @tarballs = glob("libraries/tarballs/*");

my $tarball;
my $package;
my $stamp;

for $tarball (@tarballs) {
    $package = $tarball;
    $package =~ s#^libraries/tarballs/##;
    $package =~ s/-[0-9.]*(-snapshot)?\.tar\.gz$//;

    # Sanity check, so we don't rmtree the wrong thing below
    if (($package eq "") || ($package =~ m#[/.\\]#)) {
        die "Bad package name: $package";
    }

    if (-d "libraries/$package/_darcs") {
        print "Ignoring libraries/$package as it looks like a darcs checkout\n"
    }
    else {
        $stamp = "libraries/stamp/$package";
        if ((! -d "libraries/$package") || (! -f "$stamp")
         || ((-M "libraries/stamp/$package") > (-M $tarball))) {
            print "Unpacking $package\n";
            if (-d "libraries/$package") {
                &rmtree("libraries/$package")
                    or die "Can't remove libraries/$package: $!";
            }
            mkdir "libraries/$package";
            system ("sh", "-c", "cd 'libraries/$package' && { cat ../../$tarball | gzip -d | tar xf - ; } && mv */* .") == 0
                or die "Failed to unpack $package";
            open STAMP, "> $stamp";
            close STAMP;
        }
    }
}

for $package (glob "libraries/*/") {
    $package =~ s/\/$//;
    my $pkgs = "$package/ghc-packages";
    if (-f $pkgs) {
        open PKGS, "< $pkgs";
        while (<PKGS>) {
            chomp;
            if (/.+/) {
                push @library_dirs, "$package/$_";
            }
        }
    }
    else {
        push @library_dirs, $package;
    }
}

for $package (@library_dirs) {
    my $dir = &basename($package);
    my @cabals = glob("$package/*.cabal");
    if ($#cabals > 0) {
        die "Too many .cabal file in $package\n";
    }
    if ($#cabals eq 0) {
        my $cabal = $cabals[0];
        my $pkg;
        my $stage;
        my $top;
        if (-f $cabal) {
            $pkg = $cabal;
            $pkg =~ s#.*/##;
            $pkg =~ s/\.cabal$//;
            if (-f "$package/ghc-stage") {
                open STAGE, "< $package/ghc-stage";
                $stage = int(<STAGE>);
                close STAGE;
            }
            else {
                $stage = 1;
            }
            $top = $package;
            $top =~ s#[^/]+#..#g;

            print "Creating $package/ghc.mk\n";
            open GHCMK, "> $package/ghc.mk";
            print GHCMK "${package}_PACKAGE = ${pkg}\n";
            print GHCMK "${package}_dist-install_GROUP = libraries\n";
            print GHCMK "\$(eval \$(call build-package,${package},dist-install,${stage}))\n";
            close GHCMK;

            print "Creating $package/GNUmakefile\n";
            open GNUMAKEFILE, "> $package/GNUmakefile";
            print GNUMAKEFILE "dir = ${package}\n";
            print GNUMAKEFILE "TOP = ${top}\n";
            print GNUMAKEFILE "include \$(TOP)/mk/sub-makefile.mk\n";
            print GNUMAKEFILE "FAST_MAKE_OPTS += stage=0\n";
            close GNUMAKEFILE;
        }
    }
}


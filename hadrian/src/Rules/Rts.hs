{-# LANGUAGE MultiWayIf #-}

module Rules.Rts (rtsRules, needRtsSymLinks) where

import qualified Data.Set as Set

import Packages (rts)
import Hadrian.Utilities
import Settings.Builders.Common

-- | This rule has priority 3 to override the general rule for generating shared
-- library files (see Rules.Library.libraryRules).
rtsRules :: Rules ()
rtsRules = priority 3 $ do
    -- Dynamic RTS library files need symlinks without the dummy version number.
    -- This is for backwards compatibility (the old make build system omitted the
    -- dummy version number).
    root <- buildRootRules
    [ root -/- "**/libHSrts_*-ghc*.so",
      root -/- "**/libHSrts_*-ghc*.dylib",
      root -/- "**/libHSrts-ghc*.so",
      root -/- "**/libHSrts-ghc*.dylib"]
      |%> \ rtsLibFilePath' -> createFileLink
            (addRtsDummyVersion $ takeFileName rtsLibFilePath')
            rtsLibFilePath'
    -- An import lib for the ghc-internal dll, to be linked into the rts dll.
    forM_ [Stage1, Stage2, Stage3] $ \stage -> do
        let buildPath = root -/- buildDir (rtsContext stage)
        buildPath -/- "libHSghc-internal.dll.a" %> buildGhcInternalImportLib

-- Solve the recursive dependency between rts and ghc-internal on windows
-- with an import lib libHSghc-internal.dll.a, to be linked into the rts dll.
buildGhcInternalImportLib :: FilePath -> Action ()
buildGhcInternalImportLib target = do
    let input  = "rts/win32/libHSghc-internal.def"
        output = target -- the .dll.a import lib
    need [input]
    runBuilder Dlltool ["-d", input, "-l", output] [input] [output]

-- Need symlinks generated by rtsRules.
needRtsSymLinks :: Stage -> Set.Set Way -> Action ()
needRtsSymLinks stage rtsWays
    = forM_ (Set.filter (wayUnit Dynamic) rtsWays) $ \ way -> do
        let ctx = Context stage rts way Final
        distDir     <- distDynDir ctx
        rtsLibFile  <- takeFileName <$> pkgLibraryFile ctx
        need [removeRtsDummyVersion (distDir </> rtsLibFile)]

prefix, versionlessPrefix :: String
versionlessPrefix = "libHSrts"
prefix = versionlessPrefix ++ "-1.0.3"

-- removeRtsDummyVersion "a/libHSrts-1.0-ghc1.2.3.4.so"
--                    == "a/libHSrts-ghc1.2.3.4.so"
removeRtsDummyVersion :: FilePath -> FilePath
removeRtsDummyVersion = replaceLibFilePrefix prefix versionlessPrefix

-- addRtsDummyVersion "a/libHSrts-ghc1.2.3.4.so"
--                 == "a/libHSrts-1.0-ghc1.2.3.4.so"
addRtsDummyVersion :: FilePath -> FilePath
addRtsDummyVersion = replaceLibFilePrefix versionlessPrefix prefix

replaceLibFilePrefix :: String -> String -> FilePath -> FilePath
replaceLibFilePrefix oldPrefix newPrefix oldFilePath = let
    oldFileName = takeFileName oldFilePath
    newFileName = maybe
        (error $ "Expected RTS library file to start with " ++ oldPrefix)
        (newPrefix ++)
        (stripPrefix oldPrefix oldFileName)
    in replaceFileName oldFilePath newFileName
